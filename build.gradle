buildscript {
	ext {
		springBootVersion = '1.4.0.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") 
	}
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot' 

jar {
  baseName = 'spring-mvc-4'
  version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
}

configurations {
  testCompile {
	exclude module: 'selenium-safari-driver'
	exclude module: 'selenium-ie-driver'
	//exclude module: 'selenium-firefox-driver'
	exclude module: 'selenium-htmlunit-driver'
	exclude module: 'selenium-chrome-driver'
  }
}

dependencies {
  compile "org.springframework.boot:spring-boot-configuration-processor"
//  compile "org.springframework.boot:spring-boot-devtools"
  compile "org.springframework.boot:spring-boot-starter-web"
  compile "org.springframework.boot:spring-boot-starter-thymeleaf"
  compile "org.springframework.boot:spring-boot-starter-social-twitter"
  compile 'org.springframework.boot:spring-boot-starter-security'
  
  compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity4'
  compile 'org.springframework.boot:spring-boot-starter-redis'
  compile 'org.springframework.session:spring-session'

  compile "org.webjars:materializecss:0.97.7"
  compile "org.webjars:jquery:3.1.0"
  compile "org.projectlombok:lombok:1.16.10"
  compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
  compile 'io.springfox:springfox-swagger2:2.5.0'
  compile 'io.springfox:springfox-swagger-ui:2.5.0'
  compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    
  testCompile 'org.springframework.boot:spring-boot-starter-test' 
  testCompile 'com.jayway.jsonpath:json-path'
  testCompile 'org.assertj:assertj-core:3.5.2'
  testCompile 'org.fluentlenium:fluentlenium-assertj:1.0.0-rc3'
  testCompile 'com.codeborne:phantomjsdriver:1.3.0'
  testCompile 'org.seleniumhq.selenium:selenium-java:2.53.1'
  testCompile 'org.codehaus.groovy:groovy-all:2.4.4:indy'
  testCompile 'org.spockframework:spock-spring'
  testCompile 'org.gebish:geb-spock:0.13.1'
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.13'
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
  integrationTest {
	compileClasspath += main.output + test.output
	runtimeClasspath += main.output + test.output
  }
}

task integrationTest(type: Test) {
  testClassesDir = sourceSets.integrationTest.output.classesDir
  classpath = sourceSets.integrationTest.runtimeClasspath
  reports.html.destination = file("${reporting.baseDir}/integrationTests")
}

task stage(type: Copy, dependsOn: [clean, build]) {
    from jar.archivePath
    into project.rootDir
    rename {
        'app.jar'
    }
}
stage.mustRunAfter(clean)

clean << {
    project.file('app.jar').delete()
}